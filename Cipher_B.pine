// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© vumanchu
//@version=6


//  Thanks to dynausmaux for the code
//  Thanks to falconCoin for https://www.tradingview.com/script/KVfgBvDd-Market-Cipher-B-Free-version-with-Buy-and-sell/ inspired me to start this.
//  Thanks to LazyBear for WaveTrend Oscillator https://www.tradingview.com/script/2KE8wTuF-Indicator-WaveTrend-Oscillator-WT/
//  Thanks to RicardoSantos for https://www.tradingview.com/script/3oeDh0Yq-RS-Price-Divergence-Detector-V2/
//  Thanks to LucemAnb for Plain Stochastic Divergence https://www.tradingview.com/script/FCUgF8ag-Plain-Stochastic-Divergence/
//  Thanks to andreholanda73 for MFI+RSI Area https://www.tradingview.com/script/UlGZzUAr/
//  Original code from VuManChu Cipher A 
//  I especially want to thank TradingView for its platform that facilitates development and learning.

//
//  CIRCLES & TRIANGLES:
//    - LITTLE CIRCLE: They appear at all WaveTrend wave crossings.
//    - GREEN CIRCLE: The wavetrend waves are at the oversold level and have crossed up (bullish).
//    - RED CIRCLE: The wavetrend waves are at the overbought level and have crossed down (bearish).
//    - GOLD/ORANGE CIRCLE: When RSI is below 20, WaveTrend waves are below or equal to -80 and have crossed up after good bullish divergence (DONT BUY WHEN GOLD CIRCLE APPEAR).
//    - None of these circles are certain signs to trade. It is only information that can help you. 
//    - PURPLE TRIANGLE: Appear when a bullish or bearish divergence is formed and WaveTrend waves crosses at overbought and oversold points.
//
//  NOTES:
//    - I am not an expert trader or know how to program pine script as such, in fact it is my first indicator only to study and all the code is copied and modified from other codes that are published in TradingView.
//    - I am very grateful to the entire TV community that publishes codes so that other newbies like me can learn and present their results. This is an attempt to imitate Market Cipher B. 
//    - Settings by default are for 4h timeframe, divergences are more stronger and accurate. Haven't tested in all timeframes, only 2h and 4h.
//    - If you get an interesting result in other timeframes I would be very grateful if you would comment your configuration to implement it or at least check it.
//
//  CONTRIBUTIONS:
// Errors and modifications made:
//
// VWAP adapted to all temporalities
// Fixed bug of 'transp= x' (transparency) in colors
// Fixed bug that did not appear the GOLDEN DOT
// Deactive LOG + K & D and divergence in Stochastic (we will replace with another more adjusted) 
// I add white line in the 0 continues until the end
// NEW LINE * YELLOW * is a VWAP of high temporality 720 *
// New icons to diamonds ref.   
// ** Manual - https://onedrive.live.com/view.aspx?resid=5F4137EF811BEF21!7296&ithint=file%2Cdocx&authkey=!ANcOwjD5ys9AV1A
indicator(title = 'DC-B v2.5', shorttitle = 'DC-B v2.5')


// PARAMETERS {

// WaveTrend
wtShow = input.bool(true, title = 'Show WaveTrend')
wtBuyShow = input.bool(true, title = 'Show Buy dots')
wtGoldShow = input.bool(true, title = 'Show Gold dots')
wtSellShow = input.bool(true, title = 'Show Sell dots')
wtDivShow = input.bool(true, title = 'Show Div. dots')
vwapShow = input.bool(true, title = 'Show Fast WT')
wtChannelLen = input.int(9, title = 'WT Channel Length')
wtAverageLen = input.int(12, title = 'WT Average Length')
wtMASource = input.source(hlc3, title = 'WT MA Source')
wtMALen = input.int(3, title = 'WT MA Length')

// WaveTrend Overbought & Oversold lines
obLevel = input.int(53, title = 'WT Overbought Level 1')
obLevel2 = input.int(60, title = 'WT Overbought Level 2')
obLevel3 = input.int(100, title = 'WT Overbought Level 3')
osLevel = input.int(-53, title = 'WT Oversold Level 1')
osLevel2 = input.int(-60, title = 'WT Oversold Level 2')
osLevel3 = input.int(-75, title = 'WT Oversold Level 3')

// Divergence WT
wtShowDiv = input.bool(true, title = 'Show WT Regular Divergences')
wtShowHiddenDiv = input.bool(false, title = 'Show WT Hidden Divergences')
showHiddenDiv_nl = input.bool(true, title = 'Not apply OB/OS Limits on Hidden Divergences')
wtDivOBLevel = input.int(45, title = 'WT Bearish Divergence min')
wtDivOSLevel = input.int(-65, title = 'WT Bullish Divergence min')

// Divergence extra range
wtDivOBLevel_addshow = input.bool(true, title = 'Show 2nd WT Regular Divergences')
wtDivOBLevel_add = input.int(15, title = 'WT 2nd Bearish Divergence')
wtDivOSLevel_add = input.int(-40, title = 'WT 2nd Bullish Divergence 15 min')

// RSI+MFI
rsiMFIShow = input.bool(true, title = 'Show MFI')
rsiMFIperiod = input.int(60,title = 'MFI Period')
rsiMFIMultiplier = input.float(175, title = 'MFI Area multiplier')
rsiMFIPosY = input.float(2.5, title = 'MFI Area Y Pos')

// RSI
rsiShow = input.bool(true, title = 'Show RSI')
rsiSRC = input.source(close, title = 'RSI Source')
rsiLen = input.int(14, title = 'RSI Length')
rsiOversold = input.int(30, title = 'RSI Oversold', minval = 10, maxval = 50)
rsiOverbought = input.int(70, title = 'RSI Overbought', minval = 50, maxval = 90)

// Divergence RSI
rsiShowDiv = input.bool(true, title = 'Show RSI Regular Divergences')
rsiShowHiddenDiv = input.bool(false, title = 'Show RSI Hidden Divergences')
rsiDivOBLevel = input.int(70, title = 'RSI Bearish Divergence min')
rsiDivOSLevel = input.int(30, title = 'RSI Bullish Divergence min')

// RSI Stochastic
stochShow = input.bool(true, title = 'Show Stochastic RSI')
stochUseLog = input.bool(true, title=' Use Log?')
stochAvg = input.bool(true, title='Use Average of both K & D')
stochSRC = input.source(close, title = 'Stochastic RSI Source')
stochLen = input.int(14, title = 'Stochastic RSI Length')
stochRsiLen = input.int(14, title = 'RSI Length ')
stochKSmooth = input.int(1, title = 'Stochastic RSI K Smooth')
stochDSmooth = input.int(2, title = 'Stochastic RSI D Smooth')

// Divergence stoch
stochShowDiv = input.bool(false, title = 'Show Stoch Regular Divergences')
stochShowHiddenDiv = input.bool(false, title = 'Show Stoch Hidden Divergences')

// Schaff Trend Cycle
tcLine = input.bool(false, title="Show Schaff TC line")
tcSRC = input.source(close, title = 'Schaff TC Source')
tclength = input.int(10, title="Schaff TC")
tcfastLength = input.int(23, title="Schaff TC Fast Lenght")
tcslowLength = input.int(50, title="Schaff TC Slow Length")
tcfactor = input.float(0.5, title="Schaff TC Factor")

// Sommi Flag
sommiFlagShow = input.bool(false, title = 'Show Sommi flag')
sommiShowVwap = input.bool(true, title = 'Show Sommi F. Wave')
sommiVwapTF = input.string('720', title = 'Sommi F. Wave timeframe')
sommiVwapBearLevel = input.int(0, title = 'F. Wave Bear Level (less than)')
sommiVwapBullLevel = input.int(0, title = 'F. Wave Bull Level (more than)')
soomiFlagWTBearLevel = input.int(0, title = 'WT Bear Level (more than)') 
soomiFlagWTBullLevel = input.int(0, title = 'WT Bull Level (less than)') 
soomiRSIMFIBearLevel = input.int(0, title = 'Money flow Bear Level (less than)') 
soomiRSIMFIBullLevel = input.int(0, title = 'Money flow Bull Level (more than)') 

// Sommi Diamond
sommiDiamondShow = input.bool(true, title = 'Show Sommi diamond')
sommiHTCRes = input.string('60', title = 'HTF Candle Res. 1')
sommiHTCRes2 = input.string('240', title = 'HTF Candle Res. 2')
soomiDiamondWTBearLevel = input.int(0, title = 'WT Bear Level (More than)')
soomiDiamondWTBullLevel = input.int(0, title = 'WT Bull Level (Less than)')

// macd Colors
macdWTColorsShow = input.bool(false, title = 'Show MACD Colors')
macdWTColorsTF = input.string('240', title = 'MACD Colors MACD TF')

darkMode = input.bool(false, title = 'Dark mode')

// New Inputs for Enhancements
showWT = input.bool(false, title = 'Show WaveTrend Plots')
showCloudEMAs = input.bool(true, title = 'Show Cloud EMAs')
showSmallCircles = input.bool(true, title = 'Show Small WT Cross Circles')

// Yellow Cross Thresholds
yellowCrossWTUpper = input.int(45, title = 'Yellow Cross WT Upper')
yellowCrossWTLower = input.int(-80, title = 'Yellow Cross WT Lower')
yellowCrossRSIUpper = input.int(30, title = 'Yellow Cross RSI Upper')
yellowCrossRSILower = input.int(15, title = 'Yellow Cross RSI Lower')
yellowCrossRSIMFI = input.int(-5, title = 'Yellow Cross RSI+MFI Threshold')


// Colors
colorRed = #ff0000
colorPurple = #e600e6
colorGreen = #3fff00
colorOrange = #e2a400
colorYellow = #ffe500
colorWhite = #ffffff
colorPink = #ff00f0
colorBluelight = #31c0ff

colorWT1 = #90caf9
colorWT2 = #0d47a1

colorWT2_ = #131722

colormacdWT1a = #4caf58
colormacdWT1b = #af4c4c
colormacdWT1c = #7ee57e
colormacdWT1d = #ff3535

colormacdWT2a = #305630
colormacdWT2b = #310101
colormacdWT2c = #132213
colormacdWT2d = #770000

// } PARAMETERS


// FUNCTIONS {
  
// Divergences 
//f_top_fractal(src) => src[4] < src[2] and src[3] < src[2] and src[2] > src[1] and src[2] > src[0]
//f_bot_fractal(src) => src[4] > src[2] and src[3] > src[2] and src[2] < src[1] and src[2] < src[0]
f_fractalize(src)  => src[4] < src[2] and src[3] < src[2] and src[2] > src[1] and src[2] > src[0] ? 1 :
                      src[4] > src[2] and src[3] > src[2] and src[2] < src[1] and src[2] < src[0] ? -1 : 0

f_findDivs(src, topLimit, botLimit, useLimits) =>
    fractalTop = f_fractalize(src) > 0 and (useLimits ? src[2] >= topLimit : true) ? src[2] : na
    fractalBot = f_fractalize(src) < 0 and (useLimits ? src[2] <= botLimit : true) ? src[2] : na
    highPrev = ta.valuewhen(not na(fractalTop), src[2], 0)[2]
    highPrice = ta.valuewhen(not na(fractalTop), high[2], 0)[2]
    lowPrev = ta.valuewhen(not na(fractalBot), src[2], 0)[2]
    lowPrice = ta.valuewhen(not na(fractalBot), low[2], 0)[2]
    bearSignal = not na(fractalTop) and high[2] > highPrice and src[2] < highPrev
    bullSignal = not na(fractalBot) and low[2] < lowPrice and src[2] > lowPrev
    bearDivHidden = not na(fractalTop) and high[2] < highPrice and src[2] > highPrev
    bullDivHidden = not na(fractalBot) and low[2] > lowPrice and src[2] < lowPrev
    [fractalTop, fractalBot, lowPrev, bearSignal, bullSignal, bearDivHidden, bullDivHidden]
        
// RSI+MFI
// Body Range Ratio (formerly f_rsimfi)
f_bodyRangeRatio(_period, _multiplier) => 
    ta.sma(((close - open) / math.max(high - low, 0.000001)) * _multiplier, _period)
   
// WaveTrend
f_wavetrend(src, chlen, avg, malen, tf) =>
    tfsrc = request.security(syminfo.tickerid, tf, src)
    esa = ta.ema(tfsrc, chlen)
    de = ta.ema(math.abs(tfsrc - esa), chlen)
    ci = (tfsrc - esa) / (0.015 * de)
    wt1 = request.security(syminfo.tickerid, tf, ta.ema(ci, avg))
    wt2 = request.security(syminfo.tickerid, tf, ta.sma(wt1, malen))
    wtVwap = wt1 - wt2
    wtOversold = wt2 <= osLevel
    wtOverbought = wt2 >= obLevel
    wtCross = ta.cross(wt1, wt2)
    wtCrossUp = wt2 - wt1 <= 0
    wtCrossDown = wt2 - wt1 >= 0
    wtCrosslast = ta.cross(wt1[2], wt2[2])
    wtCrossUplast = wt2[2] - wt1[2] <= 0
    wtCrossDownlast = wt2[2] - wt1[2] >= 0
    [wt1, wt2, wtOversold, wtOverbought, wtCross, wtCrossUp, wtCrossDown, wtCrosslast, wtCrossUplast, wtCrossDownlast, wtVwap]

// Schaff Trend Cycle
f_tc(src, length, fastLength, slowLength) =>
    ema1 = ta.ema(src, fastLength)
    ema2 = ta.ema(src, slowLength)
    macdVal = ema1 - ema2	
    alpha = ta.lowest(macdVal, length)
    beta = ta.highest(macdVal, length) - alpha
    gamma = (macdVal - alpha) / beta * 100
    gamma := beta > 0 ? gamma : nz(gamma[1])
    delta = gamma
    delta := na(delta[1]) ? delta : delta[1] + tcfactor * (gamma - delta[1])
    epsilon = ta.lowest(delta, length)
    zeta = ta.highest(delta, length) - epsilon
    eta = (delta - epsilon) / zeta * 100
    eta := zeta > 0 ? eta : nz(eta[1])
    stcReturn = eta
    stcReturn := na(stcReturn[1]) ? stcReturn : stcReturn[1] + tcfactor * (eta - stcReturn[1])
    stcReturn

// Stochastic RSI
f_stochrsi(_src, _stochlen, _rsilen, _smoothk, _smoothd, _log, _avg) =>
    src = _log ? math.log(_src) : _src
    rsi = ta.rsi(src, _rsilen)
    kk = ta.sma(ta.stoch(rsi, rsi, rsi, _stochlen), _smoothk)
    d1 = ta.sma(kk, _smoothd)
    avg_1 = math.avg(kk, d1)
    k = _avg ? avg_1 : kk
    [k, d1]

// MACD
f_macd(src, fastlen, slowlen, sigsmooth, tf) =>
    fast_ma = request.security(syminfo.tickerid, tf, ta.ema(src, fastlen))
    slow_ma = request.security(syminfo.tickerid, tf, ta.ema(src, slowlen))
    macd = fast_ma - slow_ma
    signal = request.security(syminfo.tickerid, tf, ta.sma(macd, sigsmooth))
    hist = macd - signal
    [macd, signal, hist]

// MACD Colors on WT    
f_macdWTColors(tf) =>
    hrsimfi = request.security(syminfo.tickerid, tf, f_bodyRangeRatio(rsiMFIperiod, rsiMFIMultiplier) - rsiMFIPosY)
    [macd, signal, hist] = f_macd(close, 28, 42, 9, macdWTColorsTF)
    macdup = macd >= signal
    macddown = macd <= signal
    macdWT1Color = macdup ? hrsimfi > 0 ? colormacdWT1c : colormacdWT1a : macddown ? hrsimfi < 0 ? colormacdWT1d : colormacdWT1b : na
    macdWT2Color = macdup ? hrsimfi < 0 ? colormacdWT2c : colormacdWT2a : macddown ? hrsimfi < 0 ? colormacdWT2d : colormacdWT2b : na 
    [macdWT1Color, macdWT2Color]
    
// Get higher timeframe candle
f_getTFCandle(_tf) => 
    _open  = request.security(ticker.heikinashi(syminfo.tickerid), _tf, open, barmerge.gaps_off, barmerge.lookahead_on)
    _close = request.security(ticker.heikinashi(syminfo.tickerid), _tf, close, barmerge.gaps_off, barmerge.lookahead_on)
    _high  = request.security(ticker.heikinashi(syminfo.tickerid), _tf, high, barmerge.gaps_off, barmerge.lookahead_on)
    _low   = request.security(ticker.heikinashi(syminfo.tickerid), _tf, low, barmerge.gaps_off, barmerge.lookahead_on)
    _hl2   = (_high + _low) / 2.0
    newBar = ta.change(_open)
    candleBodyDir = _close > _open
    [candleBodyDir, newBar]

// Sommi flag
f_findSommiFlag(tf, wt1, wt2, rsimfi, wtCross, wtCrossUp, wtCrossDown) =>    
    [hwt1, hwt2, hwtOversold, hwtOverbought, hwtCross, hwtCrossUp, hwtCrossDown, hwtCrosslast, hwtCrossUplast, hwtCrossDownlast, hwtVwap] = f_wavetrend(wtMASource, wtChannelLen, wtAverageLen, wtMALen, tf)      
    
    bearPattern = rsimfi < soomiRSIMFIBearLevel and
                   wt2 > soomiFlagWTBearLevel and 
                   wtCross and 
                   wtCrossDown and 
                   hwtVwap < sommiVwapBearLevel
                   
    bullPattern = rsimfi > soomiRSIMFIBullLevel and 
                   wt2 < soomiFlagWTBullLevel and 
                   wtCross and 
                   wtCrossUp and 
                   hwtVwap > sommiVwapBullLevel
    
    [bearPattern, bullPattern, hwtVwap]
    
f_findSommiDiamond(tf, tf2, wt1, wt2, wtCross, wtCrossUp, wtCrossDown) =>
    [candleBodyDir, newBar] = f_getTFCandle(tf)
    [candleBodyDir2, newBar2] = f_getTFCandle(tf2)
    bearPattern = wt2 >= soomiDiamondWTBearLevel and
                   wtCross and
                   wtCrossDown and
                   not candleBodyDir and
                   not candleBodyDir2                   
    bullPattern = wt2 <= soomiDiamondWTBullLevel and
                   wtCross and
                   wtCrossUp and
                   candleBodyDir and
                   candleBodyDir2 
    [bearPattern, bullPattern]
 
// } FUNCTIONS  

// CALCULATE INDICATORS {

// RSI
rsi = ta.rsi(rsiSRC, rsiLen)
rsiColor = rsi <= rsiOversold ? color.new(colorGreen,25) : rsi >= rsiOverbought ? color.new(colorRed,25) : color.new(colorPurple,25)

// RSI + MFI Area
rsiMFI = f_bodyRangeRatio(rsiMFIperiod, rsiMFIMultiplier) - rsiMFIPosY
rsiMFIColor = rsiMFI > 0 ? color.new(#3ee145,75) : color.new(#ff3d2e,75)
color rsiMFIColorT20 = rsiMFI > 0 ? color.new(#3ee145,20) : color.new(#ff3d2e,20)
color rsiMFIColorT60 = rsiMFI > 0 ? color.new(#3ee145,60) : color.new(#ff3d2e,60)

// Calculates WaveTrend
[wt1, wt2, wtOversold, wtOverbought, wtCross, wtCrossUp, wtCrossDown, wtCross_last, wtCrossUp_last, wtCrossDown_last, wtVwap] = f_wavetrend(wtMASource, wtChannelLen, wtAverageLen, wtMALen, timeframe.period)
 
// Stochastic RSI
[stochK, stochD] = f_stochrsi(stochSRC, stochLen, stochRsiLen, stochKSmooth, stochDSmooth, stochUseLog, stochAvg)

// Schaff Trend Cycle
tcVal = f_tc(tcSRC, tclength, tcfastLength, tcslowLength)

// Sommi flag
[sommiBearish, sommiBullish, hvwap] = f_findSommiFlag(sommiVwapTF, wt1, wt2, rsiMFI, wtCross,  wtCrossUp, wtCrossDown)

//Sommi diamond
[sommiBearishDiamond, sommiBullishDiamond] = f_findSommiDiamond(sommiHTCRes, sommiHTCRes2, wt1, wt2, wtCross, wtCrossUp, wtCrossDown)

// macd colors
[macdWT1Color, macdWT2Color] = f_macdWTColors(macdWTColorsTF)

// WT Divergences
[wtFractalTop, wtFractalBot, wtLow_prev, wtBearDiv, wtBullDiv, wtBearDivHidden, wtBullDivHidden] = f_findDivs(wt2, wtDivOBLevel, wtDivOSLevel, true)
    
[wtFractalTop_add, wtFractalBot_add, wtLow_prev_add, wtBearDiv_add, wtBullDiv_add, wtBearDivHidden_add, wtBullDivHidden_add] =  f_findDivs(wt2, wtDivOBLevel_add, wtDivOSLevel_add, true)
[wtFractalTop_nl, wtFractalBot_nl, wtLow_prev_nl, wtBearDiv_nl, wtBullDiv_nl, wtBearDivHidden_nl, wtBullDivHidden_nl] =  f_findDivs(wt2, 0, 0, false)

wtBearDivHidden_ = showHiddenDiv_nl ? wtBearDivHidden_nl : wtBearDivHidden
wtBullDivHidden_ = showHiddenDiv_nl ? wtBullDivHidden_nl : wtBullDivHidden

wtBearDivColor = (wtShowDiv and wtBearDiv) or (wtShowHiddenDiv and wtBearDivHidden_) ? colorRed : na
wtBullDivColor = (wtShowDiv and wtBullDiv) or (wtShowHiddenDiv and wtBullDivHidden_) ? colorGreen : na

wtBearDivColor_add = (wtShowDiv and (wtDivOBLevel_addshow and wtBearDiv_add)) or (wtShowHiddenDiv and (wtDivOBLevel_addshow and wtBearDivHidden_add)) ? #9a0202 : na
wtBullDivColor_add = (wtShowDiv and (wtDivOBLevel_addshow and wtBullDiv_add)) or (wtShowHiddenDiv and (wtDivOBLevel_addshow and wtBullDivHidden_add)) ? #1b5e20 : na

// RSI Divergences
[rsiFractalTop, rsiFractalBot, rsiLow_prev, rsiBearDiv, rsiBullDiv, rsiBearDivHidden, rsiBullDivHidden] = f_findDivs(rsi, rsiDivOBLevel, rsiDivOSLevel, true)
[rsiFractalTop_nl, rsiFractalBot_nl, rsiLow_prev_nl, rsiBearDiv_nl, rsiBullDiv_nl, rsiBearDivHidden_nl, rsiBullDivHidden_nl] = f_findDivs(rsi, 0, 0, false)

rsiBearDivHidden_ = showHiddenDiv_nl ? rsiBearDivHidden_nl : rsiBearDivHidden
rsiBullDivHidden_ = showHiddenDiv_nl ? rsiBullDivHidden_nl : rsiBullDivHidden

rsiBearDivColor = (rsiShowDiv and rsiBearDiv) or (rsiShowHiddenDiv and rsiBearDivHidden_) ? colorRed : na
rsiBullDivColor = (rsiShowDiv and rsiBullDiv) or (rsiShowHiddenDiv and rsiBullDivHidden_) ? colorGreen : na
 
// Stoch Divergences
[stochFractalTop, stochFractalBot, stochLow_prev, stochBearDiv, stochBullDiv, stochBearDivHidden, stochBullDivHidden] = f_findDivs(stochK, 0, 0, false)

stochBearDivColor = (stochShowDiv and stochBearDiv) or (stochShowHiddenDiv and stochBearDivHidden) ? colorRed : na
stochBullDivColor = (stochShowDiv and stochBullDiv) or (stochShowHiddenDiv and stochBullDivHidden) ? colorGreen : na


// Small Circles WT Cross
signalColor = wt2 - wt1 > 0 ? color.new(color.red,15) : color.new(color.lime,15)

// Buy signal.
buySignal = wtCross and wtCrossUp and wtOversold

buySignalDiv = (wtShowDiv and wtBullDiv) or 
               (wtShowDiv and wtBullDiv_add) or 
               (stochShowDiv and stochBullDiv) or 
               (rsiShowDiv and rsiBullDiv)
    
buySignalDiv_color = wtBullDiv ? color.new(colorGreen,15): 
                     wtBullDiv_add ? color.new(colorGreen, 60) : 
                     rsiShowDiv ? color.new(colorGreen,15) : na

// Sell signal
sellSignal = wtCross and wtCrossDown and wtOverbought
             
sellSignalDiv = (wtShowDiv and wtBearDiv) or 
               (wtShowDiv and wtBearDiv_add) or
               (stochShowDiv and stochBearDiv) or
               (rsiShowDiv and rsiBearDiv)
                    
sellSignalDiv_color = wtBearDiv ? color.new(colorRed,15) : 
                     wtBearDiv_add ? color.new(colorRed, 60) : 
                     rsiBearDiv ? color.new(colorRed,15) : na

// Gold Buy 
lastRsi = ta.valuewhen(not na(wtFractalBot), rsi[2], 0)[2]
wtGoldBuy = ((wtShowDiv and wtBullDiv) or (rsiShowDiv and rsiBullDiv)) and
           wtLow_prev <= osLevel3 and
           wt2 > yellowCrossWTLower and
           wt2 < yellowCrossWTUpper and
           wtLow_prev - wt2 <= -5 and
           lastRsi < yellowCrossRSIUpper and
           lastRsi > yellowCrossRSILower and
           rsiMFI < yellowCrossRSIMFI           
          
// } CALCULATE INDICATORS


// DRAW {
bgcolor(darkMode ? color.new(#000000, 80) : na)
zLine = plot(0, color = color.new(colorWhite, 0))

//  MFI BAR - Barra inferior a la grafica
hMfiTop = hline(-95, "MFI Bar Top", display=display.none)
hMfiBot = hline(-99, "MFI Bar Bot", display=display.none)
fill(hMfiTop, hMfiBot, title = 'MFI Bar Colors', color = rsiMFIShow ? rsiMFIColor : na)

// WT Plots (Removed redundant line plots, using Area plots below)

// WT Areas
color wave1Color =  macdWTColorsShow ? macdWT1Color : color.new(colorWT1,0)
color wave2Color =  macdWTColorsShow ? macdWT2Color : darkMode ? colorWT2_ : color.new(colorWT2,20)
plot(wtShow ? wt1 : na, style = plot.style_area, title = 'WT Wave 1', color = wave1Color)
plot(wtShow ? wt2 : na, style = plot.style_area, title = 'WT Wave 2', color = wave2Color)

// VWAP
plot(vwapShow ? wtVwap : na, title = 'VWAP', color = color.new(colorYellow,45), style = plot.style_area, linewidth = 2)

// MFI AREA - Cash Flow
rsiMFIplot = plot(rsiMFIShow ? rsiMFI: na, title = 'RSI+MFI Area', color = rsiMFIColorT20)
fill(rsiMFIplot, zLine, rsiMFIColorT60 )

// WT Div

plot(series = not na(wtFractalTop) ? wt2[2] : na, title = 'WT Bearish Divergence', color = wtBearDivColor, linewidth = 2, offset = -2)
plot(series = not na(wtFractalBot) ? wt2[2] : na, title = 'WT Bullish Divergence', color = wtBullDivColor, linewidth = 2, offset = -2)

// WT 2nd Div
plot(series = not na(wtFractalTop_add) ? wt2[2] : na, title = 'WT 2nd Bearish Divergence', color = wtBearDivColor_add, linewidth = 2, offset = -2)
plot(series = not na(wtFractalBot_add) ? wt2[2] : na, title = 'WT 2nd Bullish Divergence', color = wtBullDivColor_add, linewidth = 2, offset = -2)

// RSI
plot(rsiShow ? rsi : na, title = 'RSI', color = rsiColor, linewidth = 2)

// RSI Div
plot(series = not na(rsiFractalTop) ? rsi[2] : na, title='RSI Bearish Divergence', color = rsiBearDivColor, linewidth = 1, offset = -2)
plot(series = not na(rsiFractalBot) ? rsi[2] : na, title='RSI Bullish Divergence', color = rsiBullDivColor, linewidth = 1, offset = -2)

// Stochastic RSI
stochKplot = plot(stochShow ? stochK : na, title = 'Stoch K', color = color.new(#21baf3, 0), linewidth = 1)
stochDplot = plot(stochShow ? stochD : na, title = 'Stoch D', color = color.new(#673ab7, 100), linewidth = 1)
stochFillColor = stochK >= stochD ? color.new(#21baf3, 100) : color.new(#673ab7, 100)
fill(stochKplot, stochDplot, title='KD Fill', color=stochFillColor)

// Stoch Div
plot(series = not na(stochFractalTop) ? stochK[2] : na, title='Stoch Bearish Divergence', color = stochBearDivColor, linewidth = 1, offset = -2)
plot(series = not na(stochFractalBot) ? stochK[2] : na, title='Stoch Bullish Divergence', color = stochBullDivColor, linewidth = 1, offset = -2)

// Schaff Trend Cycle
plot(tcLine ? tcVal : na, color = color.new(#673ab7, 25), linewidth = 2, title = "Schaff Trend Cycle")


// Draw Overbought & Oversold lines
plot(obLevel, title = 'Over Bought Level 1', color = color.new(colorWhite,35), linewidth = 1, style = plot.style_cross)
plot(osLevel, title = 'Over Sold Level 1', color = color.new(colorWhite,35), linewidth = 1, style = plot.style_cross)

// Sommi flag
plotchar(sommiFlagShow and sommiBearish ? 108 : na, title = 'Sommi bearish flag', char='âš‘', color = colorPink, location = location.absolute, size = size.tiny)
plotchar(sommiFlagShow and sommiBullish ? -108 : na, title = 'Sommi bullish flag', char='âš‘', color = colorBluelight, location = location.absolute, size = size.tiny)
plot(sommiShowVwap ? ta.ema(hvwap, 3) : na, title = 'Sommi higher VWAP', color = color.new(#e5ff00,15), linewidth = 1, style = plot.style_line)

// Cloud EMAs (Removed to fix scaling issues on oscillator)


// Sommi diamond
plotchar(sommiDiamondShow and sommiBearishDiamond ? 108 : na, title = 'Sommi bearish diamond', char='ðŸ©¸', color = colorPink, location = location.absolute, size = size.tiny)
plotchar(sommiDiamondShow and sommiBullishDiamond ? -108 : na, title = 'Sommi bullish diamond', char='ðŸš€', color = colorBluelight, location = location.absolute, size = size.tiny)

// Circles
plot(wtCross and showSmallCircles ? wt2 : na, title = 'Buy and sell circle', color = signalColor, style = plot.style_circles, linewidth = 3)

plotchar(wtBuyShow and buySignal ? -107 : na, title = 'Buy circle', char='Â·', color = color.new(colorGreen,50), location = location.absolute, size = size.small)
plotchar(wtSellShow and sellSignal ? 105 : na , title = 'Sell circle', char='Â·', color = color.new(colorRed,50), location = location.absolute, size = size.small)

plotchar(wtDivShow and buySignalDiv ? -106 : na, title = 'Divergence buy circle', char='â€¢', color = buySignalDiv_color, location = location.absolute, size = size.small, offset = -2)
plotchar(wtDivShow and sellSignalDiv ? 106 : na, title = 'Divergence sell circle', char='â€¢', color = sellSignalDiv_color, location = location.absolute, size = size.small, offset = -2)

plotchar(wtGoldBuy and wtGoldShow ? -106 : na, title = 'Gold  buy gold circle', char='â€¢', color = color.new(#fafa11,0), location = location.absolute, size = size.small, offset = -2)

hline(0, title="Zero", color=#888888, linestyle=hline.style_solid)

// } DRAW


// ALERTS {
  
// BUY
alertcondition(buySignal, 'Buy (Big green circle)', 'Green circle WaveTrend Oversold')
alertcondition(buySignalDiv, 'Buy (Big green circle + Div)', 'Buy & WT Bullish Divergence & WT Overbought')
alertcondition(wtGoldBuy, 'GOLD Buy (Big GOLDEN circle)', 'Green & GOLD circle WaveTrend Overbought')
alertcondition(sommiBullish or sommiBullishDiamond, 'Sommi bullish flag/diamond', 'Blue flag/diamond')
alertcondition(wtCross and wtCrossUp, 'Buy (Small green dot)', 'Buy small circle')

// SELL
alertcondition(sommiBearish or sommiBearishDiamond, 'Sommi bearish flag/diamond', 'Purple flag/diamond')
alertcondition(sellSignal, 'Sell (Big red circle)', 'Red Circle WaveTrend Overbought')
alertcondition(sellSignalDiv, 'Sell (Big red circle + Div)', 'Buy & WT Bearish Divergence & WT Overbought')
alertcondition(wtCross and wtCrossDown, 'Sell (Small red dot)', 'Sell small circle')

// } ALERTS
