// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© vumanchu
//@version=6

// Thanks Dynausmaux for the code
// Based on Cipher_A from falconCoin https://www.tradingview.com/script/cAw5GEAB-Market-Cipher-A-free-version-1-1/
// Thanks to LazyBear foor WaveTrend Oscillator https://www.tradingview.com/script/2KE8wTuF-Indicator-WaveTrend-Oscillator-WT/
// I just added the red diamond, blood diamond and yellowX pattern, i dont know if is exact but seems to be.
// Still need a lot of visual adjustments to look like market cipher A but it's an attempt
// Original code from VuManChu Cipher A 
// indicator(title="VuManChu Cipher A", shorttitle="VMC Cipher_A", overlay=true)
indicator(title="DC Hacendado A", shorttitle="DC_A", overlay=true, max_lines_count = 500, max_labels_count = 500)

// FUNCTIONS {

// WaveTrend
f_wavetrend(_src, _chlen, _avg, _malen) =>
    _esa = ta.ema(_src, _chlen)
    _de = ta.ema(math.abs(_src - _esa), _chlen)
    _ci = (_src - _esa) / (0.015 * _de)
    _tci = ta.ema(_ci, _avg)
    _wt1 = _tci
    _wt2 = ta.sma(_wt1, _malen)
    [_wt1, _wt2]

// 8 EMA Ribbon
f_emaRibbon(_src, _e1, _e2, _e3, _e4, _e5, _e6, _e7, _e8) =>
    _ema1 = ta.ema(_src, _e1)
    _ema2 = ta.ema(_src, _e2)
    _ema3 = ta.ema(_src, _e3)
    _ema4 = ta.ema(_src, _e4)
    _ema5 = ta.ema(_src, _e5)
    _ema6 = ta.ema(_src, _e6)
    _ema7 = ta.ema(_src, _e7)
    _ema8 = ta.ema(_src, _e8)
    [_ema1, _ema2, _ema3, _ema4, _ema5, _ema6, _ema7, _ema8]
    
f_rsimfi(_period, _multiplier, _tf) => request.security(syminfo.tickerid, _tf, ta.sma(((close - open) / (high - low)) * _multiplier, _period))
 
// } FUNCTIONS  


// PARAMETERS {

// WaveTrend
wtChannelLen = input.int(9, title = 'WT Channel Length')
wtAverageLen = input.int(13, title = 'WT Average Length')
wtMASource = input.source(hlc3, title = 'WT MA Source')
wtMALen = input.int(3, title = 'WT MA Length')

// WaveTrend Overbought & Oversold lines
obLevel = input.int(53, title = 'WT Overbought Level 1')
obLevel2 = input.int(60, title = 'WT Overbought Level 2')
obLevel3 = input.int(100, title = 'WT Overbought Level 3')
osLevel = input.int(-53, title = 'WT Oversold Level 1')
osLevel2 = input.int(-60, title = 'WT Oversold Level 2')
osLevel3 = input.int(-80, title = 'WT Oversold Level 3')

// EMA Ribbon
showRibbon = input.bool(false, "Show Ribbon")
ema1Len = input.int(5, title = "EMA 1 Length")
ema2Len = input.int(11, title = "EMA 2 Length")
ema3Len = input.int(15, title = "EMA 3 Length")
ema4Len = input.int(18, title = "EMA 4 Length")
ema5Len = input.int(21, title = "EMA 5 Length")
ema6Len = input.int(24, title = "EMA 6 Length")
ema7Len = input.int(28, title = "EMA 7 Length")
ema8Len = input.int(34, title = "EMA 8 Length")

// RSI
rsiSRC = input.source(close, title = "RSI Source")
rsiLen = input.int(7, title = "RSI Length")
rsiOversold = input.int(40, title = 'RSI Oversold', minval = 50, maxval = 100)
rsiOverbought = input.int(60, title = 'RSI Overbought', minval = 0, maxval = 50)

// RSI+MFI
rsiMFIShow = input.bool(true, title = "Show RSI+MFI")
rsiMFIperiod = input.int(60, title = 'RSI+MFI Period')
rsiMFIMultiplier = input.int(150, title = 'RSI+MFI Area multiplier')

// }


// CALCULATE INDICATORS {

// EMA Ribbon
[ema1, ema2, ema3, ema4, ema5, ema6, ema7, ema8] = f_emaRibbon(close, ema1Len, ema2Len, ema3Len, ema4Len, ema5Len, ema6Len, ema7Len, ema8Len)

// RSI 
rsi = ta.rsi(rsiSRC, rsiLen)

// Calculates WaveTrend
[wt1, wt2] = f_wavetrend(wtMASource, wtChannelLen, wtAverageLen, wtMALen)

// WaveTrend Conditions
wtOverSold = wt2 <= osLevel
wtOverBought = wt2 >= obLevel
wtCross = ta.cross(wt1, wt2)
wtCrossUp = wt2 - wt1 <= 0
wtCrossDown = wt2 - wt1 >= 0

// RSI + MFI
rsiMFI = f_rsimfi(rsiMFIperiod, rsiMFIMultiplier, timeframe.period)

// Signals
longEma = ta.crossover(ema2, ema8)
redCross = ta.crossunder(ema1, ema2)
blueTriangle = ta.crossover(ema2, ema3)
redDiamond = wtCross and wtCrossDown
yellowCross = redDiamond and wt2 < 45 and wt2 > osLevel3 and rsi < 30 and rsi > 15 and rsiMFI < -5
bloodDiamond = redDiamond and redCross
bullCandle = open > ema2 and open > ema8 and (close[1] > open[1]) and (close > open) and not redDiamond and not redCross
shortEma = ta.crossover(ema8, ema2)

// } CALCULATE INDICATORS


// DRAW {

// EMA Ribbon
ribbonDir = ema8 < ema2
colorEma = ribbonDir ? color.green : color.red
p1 = plot(ema1, color=showRibbon ? ribbonDir ? #1573d4 : #ef5350 : na, linewidth=1, title="EMA 1")
p2 = plot(ema2, color=showRibbon ? ribbonDir ? #3096ff : #ef5350 : na, linewidth=1, title="EMA 2")
plot(ema3, color=showRibbon ? ribbonDir ? #57abff : #ef5350 : na, linewidth=1, title="EMA 3")
plot(ema4, color=showRibbon ? ribbonDir ? #85c2ff : #ef5350 : na, linewidth=1, title="EMA 4")
plot(ema5, color=showRibbon ? ribbonDir ? color.yellow : color.yellow : na, linewidth=1, title="EMA 5")
plot(ema6, color=showRibbon ? ribbonDir ? #b3d9ff : #ef5350 : na, linewidth=1, title="EMA 6")
plot(ema7, color=showRibbon ? ribbonDir ? #c9e5ff : #ef5350 : na, linewidth=1, title="EMA 7")
plot(ema8, color=showRibbon ? ribbonDir ? #dfecfb : #ef5350 : na, linewidth=1, title="EMA 8")
p8 = plot(ema8, color=showRibbon ? na : colorEma, linewidth=2, title="EMA 8")
fill(p1, p2, color = color.new(#1573d4, 90))
fill(p2, p8, color = color.new(#363a45, 90))

// SHAPES

plotshape(longEma, style=shape.circle, color=#00ff00, location=location.abovebar, size=size.tiny, title="Long EMA Signal")
plotshape(shortEma, style=shape.circle, color=#ff0000, location=location.abovebar, size=size.tiny, title="Short EMA Signal")
plotshape(redCross, style=shape.xcross, color=#ff0000, location=location.abovebar, size=size.tiny, title="Red cross")
plotshape(blueTriangle, style=shape.triangleup, color=#0064ff, location=location.abovebar, size=size.small, title="Blue Triangle")
plotshape(redDiamond, style=shape.diamond, color=#ff0000, location=location.abovebar, size=size.tiny, title="Red Diamond")
plotshape(bullCandle, style=shape.diamond, color=color.yellow, location=location.abovebar, size=size.tiny, title="Bull candle")
plotshape(bloodDiamond, style=shape.diamond, color=#ff0000, location=location.abovebar, size=size.small, title="Blood Diamond")
plotshape(yellowCross, style=shape.xcross, color=color.yellow, location=location.abovebar, size=size.small, title="Yellow Cross")
// } DRAW
  
  
// ALERTS {
alertcondition(redDiamond != 0, "Red Diamond", "Red Diamond")
alertcondition(bloodDiamond != 0, "Blood Diamond", "Blood Diamond")
alertcondition(yellowCross != 0, "YellowX", "YellowX")
alertcondition(redCross != 0, "RedX", "RedX")
alertcondition(longEma != 0, "Longema", "Longema")
alertcondition(blueTriangle != 0, "Bluetriangle", "Bluetriangle")
// } ALERTS

// CLOUD EMAs
M55=ta.ema(close, 55)
M21=ta.ema(close, 21)
M200=ta.ema(close, 200)

PT1 = plot(M55,color=#02F539, linewidth=2)
PT2 = plot(M21,color=#FFF202, linewidth=2)
PT3 = plot(M200,color=#F50202, linewidth=2)
fill(PT1,PT3,color = color.new(#02F539, 90))